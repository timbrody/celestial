#!/usr/bin/perl

=head1 NAME

B<roar_update> - update ROAR record counts from Celestial

=head1 SYNOPSIS

B<roar_update> [B<options>]

=head1 DESCRIPTION

This script updates ROAR's record counts using the Celestial database.

=head1 OPTIONS

=over 8

=item B<--help>

Show this page.

=item B<--list>

(DISABLED!) List all configured repositories.

=item B<--repository>

(DISABLED!) Harvest only the repository given (use B<--list> to get a list of identifiers).

=item B<--accession>

Set accession dates using OAI data.

=item B<--verbose>

Be more verbose.

=back

=cut

use Carp;

use strict;
use warnings;
use encoding 'utf8';

binmode(STDOUT,":utf8");

use POSIX qw/:sys_wait_h strftime/;

use vars qw($VERSION $MAX_HARVESTERS $ADMINEMAIL $HTTP_TIMEOUT $DEBUG $opt_identifier $GRANULARITY $JAVA $DROID $DROID_SIG $MAX_FILE_SIZE);

$VERSION = '3.02';

use Getopt::Long;
use Pod::Usage;

use Celestial;
use Date::Parse;
use File::Temp;

our $opt_verbose;
my( $opt_list, $opt_repository, $opt_help, $opt_cfg, $opt_fork, $opt_identifier, $opt_force, $opt_fulltext, $opt_dryrun, $opt_accession );
$opt_dryrun = 0;
$opt_verbose = 0;
$opt_fork = 1;
$opt_fulltext = 1;

GetOptions(
	'verbose+' => \$opt_verbose,
	'list' => \$opt_list,
	'accession' => \$opt_accession,
	'repository=s' => \$opt_repository,
	'help' => \$opt_help,
	'cfg=s' => \$opt_cfg,
) or pod2usage("Invalid argument");

pod2usage(1) if $opt_help;

if( defined($opt_repository) && $opt_repository =~ /\D/ ) {
	pod2usage("Repository argument must be numeric.");
}

$DEBUG = $opt_verbose;

my $cfg = Celestial::Config->new( $opt_cfg );

my $dbh = Celestial::DBI->connect() or die "Unable to connect to database: $!";

if( $opt_list ) {
	my @repos = $dbh->listRepositories();

	print "id\tidentifier\n";
	for (sort { $a->id <=> $b->id } @repos) {
		printf("%d\t%s\t%s\n", $_->id, $_->identifier, $_->baseURL);
	}
	exit(0);
}

my $iar_dbh = DBI->connect("dbi:mysql:host=eprints-stats;port=3306;database=iar","iar","")
	or die "Unable to connect to ROAR database: $!";

my @repos;

if( defined($opt_repository) )
{
	my $repo = $dbh->getRepository($opt_repository)
		or die "No such repository [$opt_repository]\n";
	push @repos, $repo;
}
else
{
	@repos = $dbh->listRepositories();
}

my $sth;

my $isth = $iar_dbh->prepare("SELECT DISTINCT `id` FROM `live` WHERE `field`='oai'");
$isth->execute;
while(my( $id ) = $isth->fetchrow_array)
{
	my @days;
	my( $low, $medium, $high ) = (0,0,0);
	my( @years, $total, $accession );

	print "\nUpdating ROAR record $id\n" if $opt_verbose;

	my $bu_sth = $iar_dbh->prepare("SELECT `value` FROM `live` WHERE `field`='oai' AND `id`=$id");
	$bu_sth->execute;
	while(my( $baseURL ) = $bu_sth->fetchrow_array)
	{
		my $repo = $dbh->getRepositoryByBaseURL( $baseURL );
		next unless defined $repo;

		my $mdf = $repo->getMetadataFormat( "oai_dc" );
		next unless defined $mdf;

		print "Fetching data for interface $baseURL\n" if $opt_verbose;

		my $table = $mdf->table();

		# get the number of records deposited on each day for the last year
		my $SQL = "SELECT TO_DAYS(NOW())-TO_DAYS(`accession`) d,COUNT(*) FROM `$table` WHERE `accession` BETWEEN NOW() - INTERVAL 1 YEAR AND NOW() GROUP BY d";
		$sth = $dbh->prepare($SQL);
		$sth->execute() or die $!;

		while(my( $d, $c ) = $sth->fetchrow_array)
		{
			$days[$d] += $c;
		}
		
		# get the number of records deposited each year since 1990
		my $start_year = 1990;
		$SQL = "SELECT YEAR(`accession`) AS d,COUNT(*) AS c FROM `$table` WHERE `accession` BETWEEN ${start_year}0000000000 AND NOW() GROUP BY d";
		$sth = $dbh->prepare($SQL);
		$sth->execute() or die $!;

		while(my( $y, $c ) = $sth->fetchrow_array)
		{
			$years[$y-$start_year] += $c;
		}

		# get the earliest dated record
		if( $opt_accession )
		{
			$SQL = "SELECT DATE_FORMAT(MIN(`accession`),'$Celestial::DBI::DATE_FORMAT') FROM `$table`";
			$sth = $dbh->prepare($SQL);
			$sth->execute() or die $!;

			my( $i ) = $sth->fetchrow_array;
			$accession = $i if !defined $accession or $i lt $accession;
		}

		# get the total number of records (inc. <1990)
		$SQL = "SELECT COUNT(*) FROM `$table`";
		$sth = $dbh->prepare($SQL);
		$sth->execute() or die $!;
		$total += $sth->fetch->[0];
		$sth->finish;
	}

	# no data found!
	next unless defined $total;

	# work out the amount of "activity" (records/day)
	for(@days)
	{
		next unless defined $_;
		$low++ if $_ > 0 and $_ < 10;
		$medium++ if $_ >= 10 and $_ < 100;
		$high++ if $_ >= 100;
	}

	# pad out empty years
	for(1990..((gmtime(time))[5]+1900))
	{
		$years[$_-1990] ||= 0;
	}

	# make year counts cumulative
	my $sum = 0;
	$_ = ($sum += ($_ || 0)) for @years;

	# update ROAR
	$iar_dbh->do("LOCK TABLES `live` WRITE");
	
	my @fields = qw( activity_low activity_medium activity_high recordcount recordhistory );

	print "Updating ROAR record for $id\n" if $opt_verbose;
	print "activity_low=$low, activity_medium=$medium, activity_high=$high, recordcount=$total\n" if $opt_verbose;
	print "recordhistory=@years\n" if $opt_verbose;

	$iar_dbh->do("DELETE FROM `live` WHERE `id`=$id AND (" . join(' OR ', map { "`field`='$_'" } @fields ) . ")");
	$iar_dbh->do("INSERT INTO `live` (`id`,`field`,`value`) VALUES ($id,'activity_low',$low), ($id,'activity_medium',$medium), ($id,'activity_high',$high), ($id,'recordcount',$total), ($id,'recordhistory','".join(',',@years)."')");
	
	if( defined($accession) )
	{
		$sth = $iar_dbh->prepare("SELECT `value` FROM `live` WHERE `id`=$id AND `field`='accession' LIMIT 1");
		$sth->execute or die $!;
		my( $roar_accession ) = $sth->fetchrow_array;
		if( $accession < $roar_accession and $accession > 19900000000000 ) {
			$iar_dbh->do("DELETE FROM `live` WHERE `id`=$id AND `field`='accession'");
			$iar_dbh->do("INSERT INTO `live` VALUES ($id,'accession',?)",{},$accession);
		}
	}
	
	$iar_dbh->do("UNLOCK TABLES");
}

# clean up the statement handle
$isth->finish;
undef $isth;

$dbh->disconnect;
$iar_dbh->disconnect;
